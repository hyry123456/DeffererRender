
#include "../HLSL/ParticleInclude.hlsl"
#include "../HLSL/NoiseWaterInput.hlsl"


#pragma kernel Water_PerFrame

//逐帧刷新的方法，用来控制与渲染直接相关的数据
[numthreads(64, 1, 1)]
void Water_PerFrame(int3 gid : SV_GroupID, int index : SV_GroupIndex)
{
    int objIndex = gid.x * 64 + index;

    FluidParticle particle = _FluidParticle[objIndex];
    FluidGroup group = _FluidGroup[gid.x];

    //组的存活时间结束, 该粒子也就结束了
    if(group.dieTime <= _Time.x){
        particle.mode = 0;  //粒子标记为死亡
        group.mode = 0;     //组标记为死亡
        _FluidParticle[objIndex] = particle;
        _FluidGroup[gid.x] = group;
        return;
    }

    if(index == 0){         //只有第一个粒子才有权利更改组数据
        if(group.mode == 0){
            //粒子组未初始化时初始化
            InitialGroup(group, _FluidParticle[gid.x * 64].random.xyz); 
        }
        if(group.mode == 1){
            group.worldPos += group.nowSpeed * _Time.y;     //初始化后就根据当前组的速度来调整组位置
        }
        _FluidGroup[gid.x] = group;
    }

    //根据模式进行该粒子的位置设置
    switch(group.mode){
        case 0:
            return;
        case 1:     //组阶段，粒子位置设置在组周围
            InitialParticleBesideGroup(particle, group);
            break;
        default:
            OnFreedomParticle(particle, group);
            break;
    }
    _FluidParticle[objIndex] = particle;
}


#pragma kernel Water_PerFixFrame
//粒子重力没必要这么大
#define GRAVITY 6.8

[numthreads(64, 1, 1)]
void Water_PerFixFrame(int3 gid : SV_GroupID, int index : SV_GroupIndex){

    int objIndex = gid.x * 64 + index;

    FluidParticle particle = _FluidParticle[objIndex];
    FluidGroup group = _FluidGroup[gid.x];

    switch(particle.mode){
        case 0:     //未运行时直接退出
            return;
        case 1:     //组阶段，如果不是第一个粒子就直接退出，防止异步bug
            if(index != 0)
                return;
            UpdataGroupSpeed(group, particle.random.xyz);
            if(_GroupMode.z == 1)
                group.nowSpeed += float3(0, -GRAVITY, 0) * _Time.z;
            if(CheckCollsion(group)){
                group.mode = 2;
            }
            _FluidGroup[gid.x] = group;         //赋值组数据
            break;
        case 2:     //正常粒子，直接自由移动即可
            UpdataSpeed(particle);

            if(_Mode.w == 1)
                particle.nowSpeed += float3(0, -GRAVITY, 0) * _Time.z;
            CheckCollsion(particle);    //普通粒子碰撞
            _FluidParticle[objIndex] = particle;  //赋值单个粒子数据
            break;
    }

}


#pragma kernel BlendDepth_Nomral_Albedo

RWTexture2D<float4> _GBufferRT0;
RWTexture2D<float2> _GBufferRT1;
RWTexture2D<float4> _GBufferRT2;
RWTexture2D<float> _GBufferDepthTex;

Texture2D<float2> _NormalMap;
Texture2D<float> _WidthMap;
Texture2D<float> _WaterDepth;
float4 _WaterDepth_TexelSize;
// float _BilaterFilterFactor;
// float4 _BlurRadius;
float _MaxFluidWidth;
float _CullOff;

uint2 _GBufferPixelCount;
uint2 _WaterPixelCount;
float2 _SpecularData;
float3 _WaterColor;

// static half3 UnpackNormalOct(half2 e){
//     half3 v = half3(e.xy,1 - abs(e.x) - abs(e.y));
//     if(v.z <= 0){
//         v.xy = signNotZero(v.xy) *(1 - abs(v.yx));
//     }
//     return normalize(v);
// }

[numthreads(32, 32, 1)]
void BlendDepth_Nomral_Albedo(uint3 id : SV_DispatchThreadID){
    //大于范围就退出
    if(id.x >= _GBufferPixelCount.x || id.y >= _GBufferPixelCount.y)
        return;
    int2 waterId = ((float2)id.xy / (float2)_GBufferPixelCount) * _WaterPixelCount;

    float width = smoothstep(0, _MaxFluidWidth * 2, _WidthMap[waterId]);
    if(width < _CullOff) return;

    float2 normalOct = _GBufferRT1[id.xy];
    float2 targetNor = _NormalMap[waterId];

    float4 rt0 = _GBufferRT0[id.xy];
    float4 targetRT0 = float4(rt0.xyz * _WaterColor, _SpecularData.x);

    float4 rt2 = _GBufferRT2[id.xy];
    rt2.w = lerp(rt2.w, _SpecularData.y, width);

    float depth = _WaterDepth[waterId];
    float bufferDepth = _GBufferDepthTex[id.xy];

    _GBufferRT0[id.xy] = lerp(rt0, targetRT0, width);
    _GBufferRT1[id.xy] = lerp(normalOct, targetNor, width);
    _GBufferRT2[id.xy] = rt2;
}